Import('env', 'arch', 'cereal', 'messaging', 'common', 'gpucommon', 'visionipc', 'is_jetson')
lenv = env.Clone()

libs = [cereal, messaging, common, 'OpenCL', 'SNPE', 'symphony-cpu', 'capnp', 'zmq', 'kj', 'yuv', gpucommon, visionipc]

TEST_THNEED = False

common_src = [
  "models/commonmodel.cc",
  "runners/snpemodel.cc",
  "transforms/loadyuv.c",
  "transforms/transform.cc"
]

if arch == "aarch64" and not is_jetson:
  libs += ['gsl', 'CB', 'gnustl_shared']
  if not TEST_THNEED:
    common_src += ["thneed/thneed.cc"]
    lenv['CFLAGS'].append("-DUSE_THNEED")
    lenv['CXXFLAGS'].append("-DUSE_THNEED")
elif arch == "larch64":
  libs += ['gsl', 'CB', 'pthread', 'dl']
  if not TEST_THNEED:
    common_src += ["thneed/thneed.cc"]
    lenv['CFLAGS'].append("-DUSE_THNEED")
    lenv['CXXFLAGS'].append("-DUSE_THNEED")
else:
  libs += ['pthread']

  if is_jetson:
    # no SNPE on ubuntu
    del libs[libs.index('SNPE')]
    del libs[libs.index('symphony-cpu')]
    del common_src[common_src.index('runners/snpemodel.cc')]
    libs += ['cudart', 'rt', 'nvinfer']
    common_src += ['runners/trtmodel.cc']
    lenv['CFLAGS'].append("-DUSE_TRT_MODEL")
    lenv['CXXFLAGS'].append("-DUSE_TRT_MODEL")
    #/usr/local/cuda
    #PUBLIC ${ONNX_INCLUDE_DIR}
    #PUBLIC ${CUDA_INSTALL_DIR}/include

    #set(SAMPLE_DEP_LIBS
    #${CUDART_LIB}
    #${CUBLAS_LIB}
    #${CUDNN_LIB}
    ##nvinfer
    #${RT_LIB}
    #${CMAKE_DL_LIBS}
    #${CMAKE_THREAD_LIBS_INIT}
  else:
    common_src += ['runners/onnxmodel.cc']
    # tell runners to use onnx
    lenv['CFLAGS'].append("-DUSE_ONNX_MODEL")
    lenv['CXXFLAGS'].append("-DUSE_ONNX_MODEL")

  if arch == "Darwin":
    # fix OpenCL
    del libs[libs.index('OpenCL')]
    lenv['FRAMEWORKS'] = ['OpenCL']

    # no SNPE on Mac
    del libs[libs.index('SNPE')]
    del libs[libs.index('symphony-cpu')]
    del common_src[common_src.index('runners/snpemodel.cc')]

common = lenv.Object(common_src)

if not is_jetson:
  lenv.Program('_dmonitoringmodeld', [
      "dmonitoringmodeld.cc",
      "models/dmonitoring.cc",
    ]+common, LIBS=libs)

lenv.Program('_modeld', [
    "modeld.cc",
    "models/driving.cc",
  ]+common, LIBS=libs)

if TEST_THNEED:
  lenv.Program('thneed/debug/_thneed', [
      "thneed/thneed.cc", "thneed/debug/test.cc"
    ]+common, LIBS=libs)

