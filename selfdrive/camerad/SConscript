Import('env', 'arch', 'cereal', 'messaging', 'common', 'gpucommon', 'visionipc', 'USE_WEBCAM', 'QCOM_REPLAY', 'is_jetson')

libs = ['m', 'pthread', common, 'jpeg', 'OpenCL', cereal, messaging, 'zmq', 'capnp', 'kj', visionipc, gpucommon]

if arch == "aarch64" and not is_jetson:
  libs += ['gsl', 'CB', 'adreno_utils', 'EGL', 'GLESv3', 'cutils', 'ui']
  if QCOM_REPLAY:
    cameras = ['cameras/camera_frame_stream.cc']
  else:
    cameras = ['cameras/camera_qcom.cc']
elif arch == "larch64":
  libs += ['atomic']
  cameras = ['cameras/camera_qcom2.cc']
else:
  if USE_WEBCAM:
    libs += ['opencv_core', 'opencv_highgui', 'opencv_imgproc', 'opencv_videoio']
    env = env.Clone()
    if is_jetson:
      libs += ['opencv_video']
      env.Append(CPPPATH = '/usr/include/opencv4')
      cameras = ['cameras/camera_mipi.cc']
      env.Append(CXXFLAGS = '-DMIPI')
      env.Append(CFLAGS = '-DMIPI')
    else:
      env.Append(CPPPATH = '/usr/local/include/opencv4')
      cameras = ['cameras/camera_webcam.cc']
      env.Append(CXXFLAGS = '-DWEBCAM')
      env.Append(CFLAGS = '-DWEBCAM')
  else:
    cameras = ['cameras/camera_frame_stream.cc']

  if arch == "Darwin":
    del libs[libs.index('OpenCL')]
    del libs[libs.index(gpucommon)][gpucommon.index('GL')]
    env = env.Clone()
    env['FRAMEWORKS'] = ['OpenCL', 'OpenGL']

env.SharedLibrary('snapshot/visionipc',
  ["#selfdrive/common/visionipc.c", "#selfdrive/common/ipc.c"])

env.Program('camerad', [
    'main.cc',
    'cameras/camera_common.cc',
    'transforms/rgb_to_yuv.c',
    'imgproc/utils.cc',
    cameras,
  ], LIBS=libs)
